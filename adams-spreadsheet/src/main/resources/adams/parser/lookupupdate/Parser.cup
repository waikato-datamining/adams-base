/*
 * STANDARD ML OF NEW JERSEY COPYRIGHT NOTICE, LICENSE AND DISCLAIMER.
 *
 * Copyright (c) 1989-1998 by Lucent Technologies
 *
 * Permission to use, copy, modify, and distribute this software and its
 * documentation for any purpose and without fee is hereby granted, provided
 * that the above copyright notice appear in all copies and that both the
 * copyright notice and this permission notice and warranty disclaimer appear
 * in supporting documentation, and that the name of Lucent Technologies, Bell
 * Labs or any Lucent entity not be used in advertising or publicity pertaining
 * to distribution of the software without specific, written prior permission.
 *
 * Lucent disclaims all warranties with regard to this software, including all
 * implied warranties of merchantability and fitness. In no event shall Lucent
 * be liable for any special, indirect or consequential damages or any damages
 * whatsoever resulting from loss of use, data or profits, whether in an action
 * of contract, negligence or other tortious action, arising out of or in
 * connection with the use or performance of this software.
 *
 * Taken from this URL:
 * http://www.smlnj.org/license.html
 *
 * This license is compatible with the GNU GPL (see section "Standard ML of New
 * Jersey Copyright License"):
 * http://www.gnu.org/licenses/license-list.html#StandardMLofNJ
 */

/*
 * Copyright 1996-1999 by Scott Hudson, Frank Flannery, C. Scott Ananian
 */

package adams.parser.lookupupdate;

import java_cup.runtime.*;

import java.io.*;
import java.util.*;
import java.lang.reflect.Array;

import adams.core.*;
import adams.core.base.*;
import adams.core.logging.*;
import adams.parser.ParserHelper;

/**
 * A parser for spreadsheet queries.
 *
 * @author FracPete (fracpete at waikato dot ac dot nz)
 */

parser code {:
  /** the logger. */
  private final static Logger LOGGER = LoggingHelper.getConsoleLogger(Parser.class);
  
  /** key - value relation. */
  protected HashMap m_Symbols = new HashMap();

  /** the if key - value relation. */
  protected HashMap m_If = new HashMap();

  /** the else key - value relation. */
  protected HashMap m_Else = new HashMap();

  /** the helper. */
  protected ParserHelper m_Helper = new ParserHelper();

  /**
   * Returns the logger.
   *
   * @return the variable-value relation
   */
  public Logger getLogger() {
    return LOGGER;
  }

  /**
   * Sets the variable - value relation to use.
   *
   * @param value the variable-value relation
   */
  public void setSymbols(HashMap value) {
    m_Symbols = value;
    m_Helper.setSymbols(value);
  }

  /**
   * Returns the current variable - value relation in use.
   *
   * @return the variable-value relation
   */
  public HashMap getSymbols() {
    return m_Symbols;
  }

  /**
   * Returns the if lookup items.
   *
   * @return the if lookup items
   */
  public HashMap getIf() {
    return m_If;
  }

  /**
   * Returns the else lookup items.
   *
   * @return the else lookup items
   */
  public HashMap getElse() {
    return m_Else;
  }

  /**
   * Returns the parser helper.
   *
   * @return the helper
   */
  public ParserHelper getHelper() {
    return m_Helper;
  }
:}

terminal IF, THEN, ELSE, END;
terminal MINUS, UMINUS, PLUS, TIMES, DIVISION, EXPONENT, MODULO;
terminal ALL, ABS, SQRT, LOG, EXP, SIN, COS, TAN, RINT, FLOOR, POW, CEIL, MIN, MAX, HAS;
terminal SUBSTR, LOWERCASE, UPPERCASE, MATCHES, TRIM, LENGTH;
terminal LEFT, RIGHT, MID, REPLACE, REPLACEALL, SUBSTITUTE, FIND, CONTAINS, CONCATENATE, REPEAT, STR;
terminal CBRT, LOG10, COSH, SINH, TANH, ATAN, ATAN2, HYPOT, SIGNUM;
terminal ASSIGNMENT, SEMI, COMMA, LPAREN, RPAREN;
terminal TRUE, FALSE, LT, LE, GT, GE, EQ, NOT_EQ, NOT, AND, OR;
terminal Double NUMBER;
terminal Boolean BOOLEAN;
terminal String VARIABLE;
terminal String STRING;

non terminal expr_list, expr_part, conditional;
non terminal assignment, if_assignments, if_assignment, else_assignments, else_assignment;
non terminal Object expr;

precedence left AND, OR;
precedence left LT, LE, GT, GE, EQ, NOT_EQ;
precedence left PLUS, MINUS;
precedence left TIMES, DIVISION, MODULO;
precedence left EXPONENT;
precedence left LPAREN, RPAREN;
precedence left ALL, ABS, SQRT, LOG, EXP, SIN, COS, TAN, RINT, FLOOR, POW, CEIL, MIN, MAX, HAS;
precedence left STR, SUBSTR, LOWERCASE, UPPERCASE, MATCHES, TRIM, LENGTH;
precedence left NOT, UMINUS;

expr_list ::= expr_list expr_part | expr_part;

expr_part ::= conditional | assignment;

conditional ::=   IF expr:c THEN if_assignments END
                  {:
                    if (parser.getHelper().toBoolean(c))
                      parser.getSymbols().putAll(parser.getIf());
                    parser.getIf().clear();
                    parser.getElse().clear();
                  :}
                | IF expr:c THEN if_assignments ELSE else_assignments END
                  {:
                    if (parser.getHelper().toBoolean(c))
                      parser.getSymbols().putAll(parser.getIf());
                    else
                      parser.getSymbols().putAll(parser.getElse());
                    parser.getIf().clear();
                    parser.getElse().clear();
                  :}
                ;

assignment ::=
                 VARIABLE:v ASSIGNMENT expr:e SEMI
                 {:
                   parser.getSymbols().put(v.toString(), e);
                 :}

               | ALL LPAREN STRING:s RPAREN ASSIGNMENT expr:e SEMI
                 {:
                    BaseRegExp regex = new BaseRegExp(s);
                    for (Object key: parser.getSymbols().keySet()) {
                      if (regex.isMatch(key.toString()))
                        parser.getSymbols().put(key.toString(), e);
                    }
                 :}
               ;

if_assignments ::= if_assignments if_assignment | if_assignment;
if_assignment ::=
                 VARIABLE:v ASSIGNMENT expr:e SEMI
                 {:
                   parser.getIf().put(v.toString(), e);
                 :}

               | ALL LPAREN STRING:s RPAREN ASSIGNMENT expr:e SEMI
                 {:
                    BaseRegExp regex = new BaseRegExp(s);
                    for (Object key: parser.getSymbols().keySet()) {
                      if (regex.isMatch(key.toString()))
                        parser.getIf().put(key.toString(), e);
                    }
                    for (Object key: parser.getIf().keySet()) {
                      if (regex.isMatch(key.toString()))
                        parser.getIf().put(key.toString(), e);
                    }
                 :}
               ;

else_assignments ::= else_assignments else_assignment | else_assignment;
else_assignment ::=
                 VARIABLE:v ASSIGNMENT expr:e SEMI
                 {:
                   parser.getElse().put(v.toString(), e);
                 :}

               | ALL LPAREN STRING:s RPAREN ASSIGNMENT expr:e SEMI
                 {:
                    BaseRegExp regex = new BaseRegExp(s);
                    for (Object key: parser.getSymbols().keySet()) {
                      if (regex.isMatch(key.toString()))
                        parser.getElse().put(key.toString(), e);
                    }
                    for (Object key: parser.getElse().keySet()) {
                      if (regex.isMatch(key.toString()))
                        parser.getElse().put(key.toString(), e);
                    }
                 :}
               ;

expr ::=        LPAREN expr:c RPAREN
                {: RESULT = c; :}
              | NUMBER:n
                {: RESULT = n; :}
              | STRING:s
                {: RESULT = s; :}
              | BOOLEAN:b
                {: RESULT = b; :}
              | VARIABLE:v
                {: if (parser.getSymbols().containsKey(v.toString()))
                     RESULT = parser.getSymbols().get(v.toString());
                   else
                     throw new IllegalStateException("Unknown symbol '" + v + "'!");
                :}

              | TRUE
                {: RESULT = true; :}
              | FALSE
                {: RESULT = false; :}

              | MINUS expr:c
                {: RESULT = -parser.getHelper().toNumber(c); :}
                %prec UMINUS

              | expr:l LT expr:r
                {: RESULT = (boolean) (parser.getHelper().compare(l, r) < 0); :}
              | expr:l LE expr:r
                {: RESULT = (boolean) (parser.getHelper().compare(l, r) <= 0); :}
              | expr:l GT expr:r
                {: RESULT = (boolean) (parser.getHelper().compare(l, r) > 0); :}
              | expr:l GE expr:r
                {: RESULT = (boolean) (parser.getHelper().compare(l, r) >= 0); :}
              | expr:l EQ expr:r
                {: RESULT = (boolean) (parser.getHelper().compare(l, r) == 0); :}
              | expr:l NOT_EQ expr:r
                {: RESULT = (boolean) (parser.getHelper().compare(l, r) != 0); :}

              | NOT expr:c
                {: RESULT = !parser.getHelper().toBoolean(c); :}
              | expr:l AND expr:r
                {: RESULT = parser.getHelper().toBoolean(l) && parser.getHelper().toBoolean(r); :}
              | expr:l OR expr:r
                {: RESULT = parser.getHelper().toBoolean(l) || parser.getHelper().toBoolean(r); :}

              | MATCHES LPAREN expr:s COMMA expr:pattern RPAREN
                {: RESULT = (boolean) (parser.getHelper().toString(s).matches(parser.getHelper().toString(pattern))); :}
              | CONTAINS LPAREN expr:s COMMA expr:f RPAREN
                {: RESULT = (boolean) (parser.getHelper().toString(s).contains(parser.getHelper().toString(f))); :}
              | FIND LPAREN expr:f COMMA expr:s RPAREN
                {: RESULT = (double) (parser.getHelper().toString(s).toLowerCase().indexOf(parser.getHelper().toString(f).toLowerCase()) + 1); :}
              | FIND LPAREN expr:f COMMA expr:s COMMA expr:p RPAREN
                {: RESULT = (double) (parser.getHelper().toString(s).toLowerCase().indexOf(parser.getHelper().toString(f).toLowerCase(), parser.getHelper().toInteger(p) - 1) + 1); :}
              | SUBSTR LPAREN expr:s COMMA expr:start RPAREN
               {: RESULT = parser.getHelper().toString(s).substring(parser.getHelper().toDouble(start).intValue() - 1); :}
              | SUBSTR LPAREN expr:s COMMA expr:start COMMA expr:end RPAREN
                {: RESULT = parser.getHelper().toString(s).substring(parser.getHelper().toDouble(start).intValue() - 1, parser.getHelper().toDouble(end).intValue() - 1); :}
              | LEFT LPAREN expr:s COMMA expr:n RPAREN
                {: RESULT = parser.getHelper().left(parser.getHelper().toString(s), parser.getHelper().toInteger(n)); :}
              | MID LPAREN expr:s COMMA expr:f COMMA expr:n RPAREN
                {: RESULT = parser.getHelper().mid(parser.getHelper().toString(s), parser.getHelper().toInteger(f) - 1, parser.getHelper().toInteger(n)); :}
              | RIGHT LPAREN expr:s COMMA expr:n RPAREN
                {: RESULT = parser.getHelper().right(parser.getHelper().toString(s), parser.getHelper().toInteger(n)); :}
              | REPEAT LPAREN expr:s COMMA expr:n RPAREN
                {: RESULT = parser.getHelper().repeat(parser.getHelper().toString(s), parser.getHelper().toInteger(n)); :}
              | CONCATENATE LPAREN expr:s1 COMMA expr:s2 RPAREN
                {: RESULT = new String(parser.getHelper().toString(s1) + parser.getHelper().toString(s2)); :}
              | CONCATENATE LPAREN expr:s1 COMMA expr:s2 COMMA expr:s3 RPAREN
                {: RESULT = new String(parser.getHelper().toString(s1) + parser.getHelper().toString(s2) + parser.getHelper().toString(s3)); :}
              | CONCATENATE LPAREN expr:s1 COMMA expr:s2 COMMA expr:s3 COMMA expr:s4 RPAREN
                {: RESULT = new String(parser.getHelper().toString(s1) + parser.getHelper().toString(s2) + parser.getHelper().toString(s3) + parser.getHelper().toString(s4)); :}
              | CONCATENATE LPAREN expr:s1 COMMA expr:s2 COMMA expr:s3 COMMA expr:s4 COMMA expr:s5 RPAREN
                {: RESULT = new String(parser.getHelper().toString(s1) + parser.getHelper().toString(s2) + parser.getHelper().toString(s3) + parser.getHelper().toString(s4) + parser.getHelper().toString(s5)); :}
              | LOWERCASE LPAREN expr:s RPAREN
                {: RESULT = parser.getHelper().toString(s).toLowerCase(); :}
              | UPPERCASE LPAREN expr:s RPAREN
                {: RESULT = parser.getHelper().toString(s).toUpperCase(); :}
              | TRIM LPAREN expr:s RPAREN
                {: RESULT = parser.getHelper().toString(s).trim(); :}
              | REPLACE LPAREN expr:s COMMA expr:p COMMA expr:l COMMA expr:n RPAREN
                {: RESULT = parser.getHelper().replace(parser.getHelper().toString(s), parser.getHelper().toInteger(p) - 1, parser.getHelper().toInteger(l), parser.getHelper().toString(n)); :}
              | REPLACEALL LPAREN expr:s COMMA expr:f COMMA expr:r RPAREN
                {: RESULT = parser.getHelper().toString(s).replaceAll(parser.getHelper().toString(f), parser.getHelper().toString(r)); :}
              | SUBSTITUTE LPAREN expr:s COMMA expr:f COMMA expr:r RPAREN
                {: RESULT = parser.getHelper().substitute(parser.getHelper().toString(s), parser.getHelper().toString(f), parser.getHelper().toString(r)); :}
              | SUBSTITUTE LPAREN expr:s COMMA expr:f COMMA expr:r COMMA expr:o RPAREN
                {: RESULT = parser.getHelper().substitute(parser.getHelper().toString(s), parser.getHelper().toString(f), parser.getHelper().toString(r), parser.getHelper().toInteger(o)); :}
              | STR LPAREN expr:s RPAREN
                {: RESULT = parser.getHelper().toString(s); :}
              | STR LPAREN expr:d COMMA expr:n RPAREN
                {: RESULT = adams.core.Utils.doubleToString(parser.getHelper().toDouble(d), parser.getHelper().toInteger(n)); :}
              | STR LPAREN expr:d COMMA STRING:f RPAREN
                {: RESULT = new java.text.DecimalFormat(f).format(parser.getHelper().toDouble(d)); :}

              | ABS LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.abs(parser.getHelper().toDouble(e))); :}
              | SQRT LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.sqrt(parser.getHelper().toDouble(e))); :}
              | CBRT LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.cbrt(parser.getHelper().toDouble(e))); :}
              | LOG LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.log(parser.getHelper().toDouble(e))); :}
              | LOG10 LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.log10(parser.getHelper().toDouble(e))); :}
              | EXP LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.exp(parser.getHelper().toDouble(e))); :}
              | SIN LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.sin(parser.getHelper().toDouble(e))); :}
              | SINH LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.sinh(parser.getHelper().toDouble(e))); :}
              | COS LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.cos(parser.getHelper().toDouble(e))); :}
              | COSH LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.cosh(parser.getHelper().toDouble(e))); :}
              | TAN LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.tan(parser.getHelper().toDouble(e))); :}
              | TANH LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.tanh(parser.getHelper().toDouble(e))); :}
              | ATAN LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.atan(parser.getHelper().toDouble(e))); :}
              | ATAN2 LPAREN expr:y COMMA expr:x RPAREN
                {: RESULT = (double) (Math.atan2(parser.getHelper().toDouble(y), parser.getHelper().toDouble(x))); :}
              | HYPOT LPAREN expr:x COMMA expr:y RPAREN
                {: RESULT = (double) (Math.hypot(parser.getHelper().toDouble(x), parser.getHelper().toDouble(y))); :}
              | SIGNUM LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.signum(parser.getHelper().toDouble(e))); :}
              | RINT LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.rint(parser.getHelper().toDouble(e))); :}
              | FLOOR LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.floor(parser.getHelper().toDouble(e))); :}
              | POW LPAREN expr:b COMMA expr:e RPAREN
                {: RESULT = (double) (Math.pow(parser.getHelper().toDouble(b), parser.getHelper().toDouble(e))); :}
              | CEIL LPAREN expr:e RPAREN
                {: RESULT = (double) (Math.ceil(parser.getHelper().toDouble(e))); :}
              | MIN LPAREN expr:e1 COMMA expr:e2 RPAREN
                {: RESULT = (double) (Math.min(parser.getHelper().toDouble(e1), parser.getHelper().toDouble(e2))); :}
              | MAX LPAREN expr:e1 COMMA expr:e2 RPAREN
                {: RESULT = (double) (Math.max(parser.getHelper().toDouble(e1), parser.getHelper().toDouble(e2))); :}
              | HAS LPAREN VARIABLE:v RPAREN
                {: RESULT = (boolean) (parser.getSymbols().containsKey(v.toString())); :}
              | LENGTH LPAREN expr:s RPAREN
                {:
                   if (s.getClass().isArray())
                     RESULT = (double) (Array.getLength(s));
                   else
                     RESULT = (double) (parser.getHelper().toString(s).length());
                :}

              | expr:l PLUS expr:r
                {: RESULT = (double) (parser.getHelper().toNumber(l) + parser.getHelper().toNumber(r)); :}
              | expr:l MINUS expr:r
                {: RESULT = (double) (parser.getHelper().toNumber(l) - parser.getHelper().toNumber(r)); :}
              | expr:l TIMES expr:r
                {: RESULT = (double) (parser.getHelper().toNumber(l) * parser.getHelper().toNumber(r)); :}
              | expr:l DIVISION expr:r
                {: RESULT = (double) (parser.getHelper().toNumber(l) / parser.getHelper().toNumber(r)); :}
              | expr:l MODULO expr:r
                {: RESULT = (double) (parser.getHelper().toNumber(l) % parser.getHelper().toNumber(r)); :}
              | expr:b EXPONENT expr:e
                {: RESULT = (double) (Math.pow(parser.getHelper().toNumber(b), parser.getHelper().toNumber(e))); :}
              ;
